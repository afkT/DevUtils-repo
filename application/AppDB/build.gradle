apply from: rootProject.file(files.build_app_gradle)
apply from: rootProject.file(files.build_router_app_gradle)
apply plugin: 'org.greenrobot.greendao'

android {
    namespace "afkt.db"

    defaultConfig {
        applicationId "afkt.db"

        // 指定 room.schemaLocation 生成的文件路径
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation":
                                      "$projectDir/schemas".toString()]
            }
        }
    }

    // https://greenrobot.org/greendao/documentation/
    // https://greenrobot.org/greendao/documentation/javadoc/
    greendao {
        // https://greenrobot.org/greendao/documentation/modelling-entities/
        schemaVersion 1

//        // Upgrade
//        schemaVersion 2

        daoPackage 'gen.greendao' // 生成 DAOs、DaoMaster、DaoSession 包名
        targetGenDir 'src/main/java'
    }
}

dependencies {

    // ==========
    // = kotlin =
    // ==========

    api deps.kotlin.room
    api deps.kotlin.room_runtime
    kapt deps.kotlin.room_compiler

    // ============
    // = androidx =
    // ============

    api deps.androidx.multidex

    // ================
    // = Dev 系列开发库 =
    // ================

    // DevSimple - 简单敏捷开发库
    api deps.dev.dev_simple

    // ============
    // = 快捷开发库 =
    // ============

    // 标题栏框架 https://github.com/getActivity/TitleBar
    api deps_lib.common_widget.titleBar
    // GreenDAO ORM 框架 https://github.com/greenrobot/greenDAO
    api deps_lib.database.greenDAO
    // 下拉刷新框架 https://github.com/scwang90/SmartRefreshLayout
    api deps_lib.common_widget.smartrefreshlayout
    api deps_lib.common_widget.smartrefresh_header_classics
    api deps_lib.common_widget.smartrefresh_footer_classics
}

// https://github.com/greenrobot/greenDAO/issues/1110
tasks.configureEach { task
    ->
    if (task.name.matches("\\w*compile\\w*Kotlin")) {
        task.dependsOn('greendao')
    }
    if (task.name.matches("\\w*kaptGenerateStubs\\w*Kotlin")) {
        task.dependsOn('greendao')
    }

    if (task.name.matches("\\w*kapt\\w*Kotlin")) {
        task.dependsOn('greendao')
    }
}