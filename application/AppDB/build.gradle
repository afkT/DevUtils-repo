apply from: rootProject.file(files.build_app_gradle)
apply from: rootProject.file(files.build_router_app_gradle)
apply plugin: 'org.greenrobot.greendao'

android {
    namespace "afkt.db"

    defaultConfig {
        applicationId "afkt.db"

        // 指定 room.schemaLocation 生成的文件路径
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation":
                                      "$projectDir/schemas".toString()]
            }
        }
    }

    // https://greenrobot.org/greendao/documentation/
    // https://greenrobot.org/greendao/documentation/javadoc/
    greendao {
        // https://greenrobot.org/greendao/documentation/modelling-entities/
        schemaVersion 1

//        // Upgrade
//        schemaVersion 2

        daoPackage 'gen.greendao' // 生成 DAOs、DaoMaster、DaoSession 包名
        targetGenDir 'src/main/java'
    }

    sourceSets {
        main {
            // https://www.jianshu.com/p/f5a49d54e16e
            // 优化 res 资源管理, 使用 sourceSets 资源分包
            res.srcDirs = [
                    'src/main/res', // 全局通用资源文件
                    'src/main/res-base', // Base 基础资源
                    'src/main/res-db', // DB 基础资源
            ]
        }
    }
}

dependencies {

    // ==========
    // = kotlin =
    // ==========

    api deps.kotlin.room
    api deps.kotlin.room_runtime
    kapt deps.kotlin.room_compiler

    // ============
    // = androidx =
    // ============

    api deps.androidx.multidex

    // ================
    // = Dev 系列开发库 =
    // ================

    // DevBase - Base ( Activity、Fragment )、MVP、ViewBinding、ContentLayout 基类库
    api deps.dev.dev_base

    // DevBaseMVVM - MVVM ( ViewDataBinding + ViewModel ) 基类库
    api deps.dev.dev_base_mvvm

    // DevMVVM - DataBinding 工具类库
    api deps.dev.dev_mvvm

    // DevSimple - 简单敏捷开发库
    api deps.dev.dev_simple

    // DevAgile - 简单敏捷开发库 ( 常用、零散代码 )
    api deps.dev.dev_agile

    // DevEngine - 第三方框架解耦、一键替换第三方库、同类库多 Engine 组件化混合使用
    api deps.dev.dev_engine

    // ============
    // = 快捷开发库 =
    // ============

    // GreenDAO ORM 框架 https://github.com/greenrobot/greenDAO
    api deps_lib.database.greenDAO
    // 下拉刷新框架 https://github.com/scwang90/SmartRefreshLayout
    api deps_lib.common_widget.smartrefreshlayout
    api deps_lib.common_widget.smartrefresh_header_classics
    api deps_lib.common_widget.smartrefresh_footer_classics
}

// https://github.com/greenrobot/greenDAO/issues/1110
tasks.configureEach { task
    ->
    if (task.name.matches("\\w*compile\\w*Kotlin")) {
        task.dependsOn('greendao')
    }
    if (task.name.matches("\\w*kaptGenerateStubs\\w*Kotlin")) {
        task.dependsOn('greendao')
    }

    if (task.name.matches("\\w*kapt\\w*Kotlin")) {
        task.dependsOn('greendao')
    }
}